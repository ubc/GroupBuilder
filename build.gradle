apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"
apply plugin: "eclipse"
apply plugin: "idea"

// Uncomment if your project has an API JAR
//apply from: "api.gradle"

// define the version for the project when publishing to maven
group "ca.ubc"
version "1.1.1"

webAppDirName = file("WebContent")
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }

    test {
        java {
            srcDir 'test'
        }
    }
}

ext {
    learnVersion = "9.1.201410.160373"
    deployServer = "127.0.0.1:9877"
    deployInstance = "BBLEARN"
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.blackboard.com/content/repositories/releases/"
    }
    maven {
        url "http://artifactory.ctlt.ubc.ca/artifactory/ctlt-release-local/"
    }
}

configurations {
    buildUtils
}

// define the project's dependencies
dependencies {
    providedCompile "javax.servlet:servlet-api:2.5",
            "javax.servlet.jsp:jsp-api:2.1"

    // Dependencies are libraries needed to build, but should not be included in the B2 WAR.
    // You should NEVER include Learn JARs (other than webapis) in your B2.
    providedCompile("blackboard.platform:bb-platform:$learnVersion") { transitive = false }
    providedCompile("blackboard.platform:bb-taglibs:$learnVersion") { transitive = false }
    providedCompile("blackboard.platform:bb-bbxythos:$learnVersion") { transitive = false }
    providedCompile("blackboard.platform:xsscore:$learnVersion") { transitive = false }

    // Building a Spring B2?  Use our Spring APIs!
    // compile "blackboard.platform:bb-spring-webapi:10.1"

    // Include the Blackboard security APIs for protecting your B2.
    // providedCompile "blackboard.plugin:security:1.0:api"

    // Dependencies that are only necessary at runtime so should be included in the WAR
    // runtime "org.javassist:javassist:3.17.1-GA"

    buildUtils "org.oscelot:b2deploy-task:0.1.0"

    testCompile 'junit:junit:4.10',
            'org.mockito:mockito-all:1.9.5-rc1',
            'com.puppycrawl.tools:checkstyle:5.5'

    compile 'commons-io:commons-io:2.4',
            'commons-fileupload:commons-fileupload:1.2.1',
            'org.apache.commons:commons-csv:1.0',
            'javax.servlet:jstl:1.2',
            'net.sf.opencsv:opencsv:2.3',
            'commons-lang:commons-lang:2.6'
}

test {
    useTestNG()
}

// Add a task to deploy a B2 using starting block
task deployB2(dependsOn: "war") << {
    ant.taskdef(name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.buildUtils.asPath)
    ant.b2deploy(localfilepath: project.war.archivePath, host: project.ext.deployServer, courseorgavailable: 'true', clean: 'true', webappName: 'bb-starting-block-' + project.ext.deployInstance)
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
